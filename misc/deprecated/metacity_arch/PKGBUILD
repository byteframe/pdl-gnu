# Maintainer : Martin Wimpress <code@flexion.org>

_ver=2.30.3
_pkgbase=metacity
pkgname=(${_pkgbase})
pkgver=${_ver}
pkgrel=1
pkgdesc="A window manager for GNOME (GTK2 version)"
url="http://gnome.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('gtk2' 'libcanberra' 'libgtop' 'libice' 'libsm' 'libxt'
         'startup-notification' 'zenity')
conflicts=("${_pkgbase}-gtk3")
source=("http://ftp.acc.umu.se/pub/gnome/sources/metacity/2.30/metacity-2.30.3.tar.bz2")
sha1sums=('aada9e6e34d9aa86771021ffca14d4f1482f2310')
install=${_pkgbase}.install

build() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --with-gtk=2.0 \
        --enable-startup-notification \
        --disable-static
    make
}

package() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}

